
buildscript {
    ext.kotlin_version = '1.0.0-beta-4584'
    ext.commons_lang3_version = '3.4'
    ext.google_gson_version = '2.5'
    ext.google_guava_version = '19.0'
    ext.slf4j_api_version = '1.7.13'

    ext.logback_version = '1.1.3'
    ext.junit_version = '4.12'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1"
    }
}

plugins {
    id "org.standardout.versioneye" version "1.1.1"
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'kotlin'

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

apply plugin: 'maven-publish'

group 'org.swordess.persistence'
version '0.1.1'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.apache.commons:commons-lang3:$commons_lang3_version"
    compile "com.google.code.gson:gson:$google_gson_version"
    compile "com.google.guava:guava:$google_guava_version"
    compile "org.slf4j:slf4j-api:$slf4j_api_version"

    testCompile "ch.qos.logback:logback-classic:$logback_version"
    testCompile "junit:junit:$junit_version"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

//jacoco {
//    toolVersion = "0.7.5.201505241946"
//}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        MyMaven(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyMaven']
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/xingyuli/jsondb.git'
        version {
            name = project.version
            released  = new Date()
        }
    }
}
